buildscript {
    ext {
        versions = [
                compileSdk           : 29,
                minSdk               : 21,
                targetSdk            : 29,

                // kotlin
                kotlin               : '1.3.70',

                // android
                core_ktx             : '1.2.0',
                appcompat            : '1.1.0',
                preference_ktx       : '1.1.0',
                constraintlayout     : '1.1.3',
                arch_version         : '2.1.0',
                lifecycle            : '2.2.0',
                navigation           : '2.2.1',
                room                 : '2.2.4',
                material             : '1.2.0-alpha05',

                // firebase
                firebase_core        : '17.2.3',
                firebase_appindexing : '19.1.0',
                firebase_perf        : '19.0.5',
                crashlytics          : '2.10.1',

                // dagger
                dagger               : '2.26',
                assisted_inject      : '0.5.2',

                // reactive
                rxjava               : '2.2.18',
                rxandroid            : '2.1.1',
                rxkotlin             : '2.4.0',
                rxbinding            : '3.1.0',
                rxpreferences        : '2.0.0',
                rxpicasso            : '0.3.0',

                // network
                okhttp               : '4.4.0',
                retrofit             : '2.7.1',
                picasso              : '3.0.0-SNAPSHOT',

                // misc
                permissionsdispatcher: '4.6.0',

                // debug
                timber               : '4.7.1',
                leakcanary           : '2.2',

                // test
                junit                : '4.13',
                test_core            : '1.2.0',
                robolectric          : '4.3.1',
                mockito              : '3.3.1',
                runner               : '1.2.0',
                espresso_core        : '3.2.0'
        ]
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:perf-plugin:1.3.1'
        classpath 'io.fabric.tools:gradle:1.31.2'
        classpath 'android.arch.navigation:navigation-safe-args-gradle-plugin:1.0.0'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.28.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

apply plugin: "com.github.ben-manes.versions"

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

