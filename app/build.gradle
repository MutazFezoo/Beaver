apply plugin: "com.android.application"
apply plugin: "com.google.firebase.firebase-perf"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "net.samystudio.beaver"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "FULL_VERSION_NAME", "\"${versionName}.${versionCode}\"")
        resValue "string", "application_id", applicationId
    }

    signingConfigs {
        release {
            keyAlias "beaver"
            keyPassword "beaver"
            storeFile file("keystore.jks")
            storePassword "beaver"
        }
    }

    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            manifestPlaceholders = [crashlyticsEnabled: true]
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kapt {
        generateStubs = false
    }

    androidExtensions {
        experimental = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def sep = "_"
            def version = variant.versionName
            def build = variant.versionCode
            outputFileName = rootProject.name + sep +
                    variant.buildType.name + sep +
                    version + sep + "build" + sep + build + ".apk"
        }
    }
}

dependencies {
    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:support-v4:27.1.0"
    implementation "com.android.support:appcompat-v7:27.1.0"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    implementation "com.google.firebase:firebase-core:11.8.0"
    implementation "com.google.firebase:firebase-perf:11.8.0"
    implementation "com.google.firebase:firebase-appindexing:11.8.0"
    implementation("com.crashlytics.sdk.android:crashlytics:2.9.1@aar") {
        transitive = true
    }
    implementation "android.arch.lifecycle:extensions:1.1.0"
    kapt "android.arch.lifecycle:compiler:1.1.0"
    implementation "android.arch.lifecycle:reactivestreams:1.1.0"
    implementation "com.google.dagger:dagger:2.14.1"
    implementation "com.google.dagger:dagger-android:2.14.1"
    implementation "com.google.dagger:dagger-android-support:2.14.1"
    kapt "com.google.dagger:dagger-compiler:2.14.1"
    kapt "com.google.dagger:dagger-android-processor:2.14.1"
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
    implementation "io.reactivex.rxjava2:rxjava:2.1.9"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "com.jakewharton.rxbinding2:rxbinding:2.1.1"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1"
    implementation "com.github.bumptech.glide:glide:4.6.1"
    implementation "com.github.bumptech.glide:okhttp3-integration:4.6.1"
    implementation "com.jakewharton.timber:timber:4.6.1"
    implementation "com.evernote:android-state:1.2.0"
}

apply plugin: "com.google.gms.google-services"